1. ABNF Query Language Specification for ARQL

alphachain  =   1*ALPHA LWSP *("." LWSP 1*ALPHA LWSP)
alias       =   1*ALPHA ":"
join        =   ["?" / "!"] LWSP "."
param       =   "$" 1*DIGIT
opchar      =   "+" /
                "-" / 
                "*" / 
                "/" / 
                "<" /
                ">" /
                "=" /
                "~" /
                "!" /
                "@" /
                "#" /
                "%" /
                "^" /
                "&" /
                "|" /
                "`" /
                "?"
op          =   1*(opchar)

expr        =   ("(" LWSP expr LWSP ")") /
                param /
                alphachain /
                ([op] LWSP expr LWSP [op] LWSP expr) ; recursive, TODO postfix operators?
exprlist    =   [*(expr LWSP "," LWSP) expr LWSP [","]]
transform   =   "|" LWSP alphachain LWSP ["(" LWSP exprlist LWSP ")"] LWSP

source      =   (( "(" [alias] from ")" ) / alphachain) LWSP *(join LWSP source LWSP)
from        =   source LWSP *transform [shape]
to          =   alphachain LWSP *transform [shape]

field       =   [alias] LWSP (from / expr)
fieldlist   =   *(field LWSP "," LWSP) field LWSP [","]
shape       =   "{" LWSP fieldlist LWSP "}"

select      =   from
insert      =   from LWSP "-+" LWSP to
update      =   from LWSP "->" LWSP to
delete      =   ("->" / "-x") LWSP to

query       =   select / insert / update / delete
