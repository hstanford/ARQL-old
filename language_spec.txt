1. ABNF Query Language Specification for ARQL

keyword     =   ALPHA *(ALPHA / DIGIT)
alphachain  =   keyword LWSP *("." LWSP keyword LWSP)
alias       =   keyword ":"
param       =   "$" 1*DIGIT
opchar      =   "+" /
                "-" / 
                "*" / 
                "/" / 
                "<" /
                ">" /
                "=" /
                "~" /
                "!" /
                "@" /
                "#" /
                "%" /
                "^" /
                "&" /
                "`" /
                "?" /
                ":" /
                "|"
op          =   1*(opchar)

exprnoop    =   ("(" LWSP expr LWSP ")") /
                param /
                alphachain /
                source
exprunary   =   (
                  (op exprunary) /
                  (exprnoop ["(" LWSP exprlist LWSP ")"])
                ) LWSP *(op LWSP)
exprop      =   LWSP 1*exprunary
expr        =   exprop / exprnoop
exprlist    =   [expr LWSP *("," LWSP expr LWSP) [","]]
trarg       =   expr / shape / source
trargs      =   [trarg LWSP *("," LWSP trarg LWSP) [","]]
transform   =   alphachain LWSP ["(" LWSP exprlist LWSP ")"]
transforms  =   *("|" LWSP transform LWSP )

source      =   [alias] LWSP (sourcelist | alphachain) LWSP transforms [shape]
sourcelist  =   "(" LWSP *(source LWSP "," LWSP) source LWSP [","] LWSP ")"
dest        =   [alias] LWSP keyword LWSP transforms [shape]

field       =   [alias] LWSP (source / expr)
fieldlist   =   *(field LWSP "," LWSP) field LWSP [","]
shape       =   "{" LWSP fieldlist LWSP "}"

modifier    =   "-" (">" / "+" / "x")

query       =   LWSP [source] LWSP [modifier LWSP dest] LWSP