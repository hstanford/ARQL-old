1. ABNF Query Language Specification for ARQL

keyword     =   ALPHA *(ALPHA / DIGIT)
alphachain  =   keyword LWSP *("." LWSP keyword LWSP)
alias       =   keyword ":"
join        =   ["?" / "!"] LWSP "."
param       =   "$" 1*DIGIT
opchar      =   "+" /
                "-" / 
                "*" / 
                "/" / 
                "<" /
                ">" /
                "=" /
                "~" /
                "!" /
                "@" /
                "#" /
                "%" /
                "^" /
                "&" /
                "|" /
                "`" /
                "?" /
                ":"
op          =   1*(opchar)

exprnoop    =   ("(" LWSP expr LWSP ")") /
                param /
                alphachain
exprunary   =   (
                  (op exprunary) /
                  (exprnoop ["(" LWSP exprlist LWSP ")"])
                ) LWSP *(op LWSP)
exprop      =   LWSP 1*exprunary
expr        =   exprOp / exprnoop
exprlist    =   [*(expr LWSP "," LWSP) expr LWSP [","]]
transform   =   "|" LWSP alphachain LWSP ["(" LWSP exprlist LWSP ")"] LWSP

source      =   (
                  ("(" LWSP fullfrom LWSP ")") /
                  alphachain
                ) LWSP *(join LWSP source LWSP)
fullfrom    =   [source] LWSP *transform [shape]
from        =   [source] LWSP *transform shape
to          =   (
                  ("(" LWSP [alias] LWSP alphachain LWSP ")") /
                  alphachain
                ) LWSP *transform [shape]

field       =   [alias] LWSP (from / expr)
fieldlist   =   *(field LWSP "," LWSP) field LWSP [","]
shape       =   "{" LWSP fieldlist LWSP "}"

delete      =   ("->" / "-x") LWSP to
modify      =   from LWSP "-" ("+" / ">") LWSP to
select      =   from

query       =   LWSP (del / modify / select) LWSP
